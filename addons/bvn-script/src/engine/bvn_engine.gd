@icon("../../icons/bvn.svg")
@tool
extends BVN_Chapter

class_name BVN_Engine

## Main Resource where the VN data is saved.
@export var visual_novel:BVN_VisualNovel:
	set(v):
		if cmd_runner:
			cmd_runner.visual_novel = v
		if speaker_runner:
			speaker_runner.visual_novel = v
		visual_novel = v

## Begins the VN Engine as soon as its ready.
@export var autoplay:bool = false

@export_group("Advanced Configuration")

## The CanvasLayer where the BVN Gui Elements are added to
## If not provided - The Engine will make one on runtime
@export var managed_canvas_override:CanvasLayer
@export var managed_canvas:CanvasLayer

var gui_canvas:CanvasLayer:
	get: return managed_canvas_override if managed_canvas_override else managed_canvas

var character_cache:Dictionary[String, BVN_CharacterSheet] = {}

var bdb := BdbThrottleByMsec.new(250)



#region services
var cmd_runner:BVNInternal_EngineCommandRunner
var speaker_runner:BVNInternal_SpeakerRunner

var gui_orchestrator:BvnInternal_GuiOrchestrator 
var session_service:BVNInternal_SessionService 
var managed_node_service:BVNInernal_ManagedNodeService
var lock_service:BVNInternal_LockService
#endregion


const AST_TYPE_UNSPECIFIED = Bvn_AstNode.TYPE_UNSPECIFIED
const AST_TYPE_NOOP = Bvn_AstNode.TYPE_NOOP
const AST_TYPE_SPEAK = Bvn_AstNode.TYPE_SPEAK
const AST_TYPE_COMMAND = Bvn_AstNode.TYPE_COMMAND
const AST_TYPE_IF = Bvn_AstNode.TYPE_IF
const AST_TYPE_ELSE_IF = Bvn_AstNode.TYPE_ELSE_IF
const AST_TYPE_ELSE = Bvn_AstNode.TYPE_ELSE

var service_node:Node

var debouncers:Dictionary[String,BdbDebounce] = {}
var settings:BVN_SettingsNode
func _init() -> void:
	tree_entered.connect(func ():
		add_to_group(BVNInternal_Tags.ENGINE)
		
		if Engine.is_editor_hint():
			BVN_ProjectSettings.last_edited_engine = EditorInterface.get_edited_scene_root().scene_file_path
		
		if service_node == null:
			service_node = Node.new()
			service_node.name = "BVNI Services"
			add_child(service_node, true,Node.INTERNAL_MODE_BACK)
			
		#region CMD_RUNNER setup
		if cmd_runner == null:
			cmd_runner = BVNInternal_EngineCommandRunner.new()
			service_node.add_child(cmd_runner, true,Node.INTERNAL_MODE_BACK)
		#endregion
		
		#region SPEAKER_RUNNER
		if speaker_runner == null:
			speaker_runner = BVNInternal_SpeakerRunner.new()
			service_node.add_child(speaker_runner, true,Node.INTERNAL_MODE_BACK)
		#endregion
		
		#region SERVICES
		if session_service == null:
			session_service = BVNInternal_SessionService.new()
			service_node.add_child(session_service, true,Node.INTERNAL_MODE_BACK)
		if managed_node_service == null:
			managed_node_service = BVNInernal_ManagedNodeService.new()
			service_node.add_child(managed_node_service, true,Node.INTERNAL_MODE_BACK)
		if lock_service == null:
			lock_service = BVNInternal_LockService.new()
			service_node.add_child(lock_service, true,Node.INTERNAL_MODE_BACK) 
			
		## Do not hide this node, we want to surface it to the user.
		settings = BVNInternal.ensure_child(self, BVN_SettingsNode, Node.INTERNAL_MODE_DISABLED)
		settings.name = "Visual Novel Settings"
		#endregion 
		
		#region 
		if managed_canvas_override == null and managed_canvas == null:
			managed_canvas = CanvasLayer.new()
			managed_canvas.name = "BVN Internal Autogenerated Canvas"
			managed_canvas.layer = int(2147483647) # Max int
			add_child(managed_canvas, true)
		gui_orchestrator = BvnInternal_GuiOrchestrator.new()
		gui_orchestrator.gui_canvas = gui_canvas
		gui_orchestrator.name = "BvnInternal_GuiOrchestrator"
		service_node.add_child(gui_orchestrator, true, Node.INTERNAL_MODE_DISABLED)
		gui_orchestrator.owner = owner
		#endregion
		
		#region add debouncers
		debouncers.load = BdbDebounce.new(0.15)	
		service_node.add_child(debouncers.load, true, Node.INTERNAL_MODE_DISABLED)
		#endregion
		
		visual_novel = visual_novel # Force trigger propagations of visual novel
		BVN.engine = self
		)
	tree_exited.connect(func ():
		# Free all services
		if is_instance_valid(service_node): 
			service_node.free()
		)
		
		
	ready.connect(func ():
		if !Engine.is_editor_hint():
			BVN_EventBus.on_request_next_engine_action.connect(next)
			BVN_EventBus.on_request_load_session.connect(debouncers.load.cb(_load_game).handle, CONNECT_DEFERRED)
			if autoplay: 
				get_tree().create_timer(0.1).timeout.connect(start_visual_novel)
		)

func _load_game(..._unused:Array):
	start_visual_novel()

func _get_configuration_warnings() -> PackedStringArray:
	var errors:Array[String] = []
	if visual_novel == null:
		errors.append("Please add a visual novel resource")
		return errors
		
	if !visual_novel.characters:
		errors.append("No characters added to visual novel resource")
		
	var found_store:Array = BdbSelect.children_by_type(self, BVN_Variables)
	if not(found_store) or found_store.size() != 1:
		errors.append("Must have exactly one '%s' child" % (BVN_Variables as Script).get_global_name())
		
	var page = BdbSelect.child_by_type(self, BVN_Page)
	var chapter = BdbSelect.child_by_type(self, BVN_Chapter)
	if not(page or chapter):
		errors.append("Must have atleast one '%s' or '%s' child" % [
			(BVN_Page as Script).get_global_name(),
			(BVN_Chapter as Script).get_global_name()
		])

	return errors
	
var has_started := false
func start_visual_novel():
	has_started = true
	var page:BVN_Page = null
	#region FROM LOAD REQUEST
	if managed_node_service.page_context:
		page = managed_node_service.page_context.page
	#endregion
		
	#region NO STARTING
	elif managed_node_service:
		page = BdbSelect.item_by_type(managed_node_service.pages, BVN_Page)
	#endregion
	assert(page, "Couldn't find a page to use")
	run_page(page)

## Context of the current running scene
var context:BVNInternal_SceneExecutionContext
func run_page(page:BVN_Page):
	context = managed_node_service.mk_page_context()
	
	var script := page.page_data.scene_script
	assert(script, "'%s' has no script. Start writing one now" % page.get_page_path())
	var root_node := script_parser.parse_bvn_script(script)
	
	context.node_data = {}
	context.page = page
	context.current_node = root_node
	
	managed_node_service.push_page(page)
	next()

func next(next_node:Bvn_AstNode = null):
	if (
		lock_service.is_locked or 
		(!Engine.is_editor_hint() and !has_started)
	): 
		return
	
	var prev_node := context.current_node # Just storing for debugging reasons
	
	# Attempt 1: Override next AST node to execute
	# Attempt 2: Get last AST's sibling
	if next_node == null and context.current_node: # Has Override
		var tmp := context.current_node.get_next_node()
		if tmp:
			next_node = context.current_node.get_next_node()
	context.current_node = next_node
	# Attempt 3: If nothing found, then we've exhausted all the commands
	# and we move on to the next page.
	if next_node == null: # 
		var next_page = BdbSelect.next_sibling_by_type(context.page, BVN_Page)
		if next_page:
			run_page(next_page)
		else:
			printerr(
			"You are at the end of the Chapter. Specify a transition action e.g.:" +
			'\n\t > goto.next_chapter()' +
			'\n\t > goto.chapter("Chapter 1 / Page 932")' +
			'\n\t > %"Page12".show()')
		return

	var vars = BVNInternal_Query.variables.get_format_payload()
	vars[&".page_path"] = context.page.get_page_path()
	execute_bvn_instruction(context.current_node, vars)
	
var script_parser:BVN_ScriptParser = BVN_ScriptParser.new()

## Returns an error string if any
const IF_ELSE_LIST = [Bvn_AstNode.TYPE_IF, Bvn_AstNode.TYPE_ELSE_IF]
enum IF_ELSE_STATE {
	FAILED = 0,
	PASSED = 1,
	SKIPPED = 2, 
}

func execute_bvn_instruction(ast_node:Bvn_AstNode, vars:Dictionary = {}):
	var is_run_in_game = context # context gets created on PLAY only
				
	match ast_node.type:
		AST_TYPE_SPEAK:
			speaker_runner.load_ast_node(ast_node, vars)
			speaker_runner.run()
		AST_TYPE_COMMAND:
			var nodes_to_run:Array[Bvn_AstNode] = [ast_node]
			nodes_to_run.append_array(ast_node.children)
			
			for node_to_run in nodes_to_run:
				var result: = cmd_runner.execute_node(node_to_run, vars, context.page if context else null)
				assert(result[0] == OK, "There was an error running the command '%s'" % node_to_run.text)

			if is_run_in_game:
				next(ast_node.get_next_node(false))
				
		AST_TYPE_IF:
			var collapse_result := cmd_runner.execute_ifelse_node(ast_node, vars, context.page)
				
			if is_run_in_game:
				if collapse_result:
					context.node_data[ast_node] = {&"result": IF_ELSE_STATE.PASSED}
					next(ast_node.get_next_node())
				else:
					context.node_data[ast_node] = {&"result": IF_ELSE_STATE.FAILED}
					var next_sibling := ast_node.get_next_sibling()
					if next_sibling:
						next(next_sibling)
					else:
						next(ast_node.get_next_node())
						
		AST_TYPE_ELSE_IF:
			if is_run_in_game:
				
				var prev_sibling := ast_node.get_prev_sibling()
				if prev_sibling.type not in IF_ELSE_LIST:
					return &"'elif' without matching 'if' statement"
				var previous_sibling_result:IF_ELSE_STATE = context.node_data[prev_sibling].result
				if previous_sibling_result == IF_ELSE_STATE.FAILED:
					# only run node if prev is failed
					var collapse_result := cmd_runner.execute_ifelse_node(ast_node, vars, context.page)
					if collapse_result:
						context.node_data[ast_node] = {&"result":IF_ELSE_STATE.PASSED}
						next(ast_node.get_next_node())
						return &""

				# if reached this point, it means we've attempted to evaluate or previous is a pass
				context.node_data[ast_node] = {&"result":IF_ELSE_STATE.SKIPPED}
				var next_sibling := ast_node.get_next_sibling()
				if next_sibling:
					next(next_sibling)
				else:
					next(ast_node.get_next_node())
				
		AST_TYPE_ELSE:
			if is_run_in_game:
				var prev_sibling := ast_node.get_prev_sibling()
				if prev_sibling.type not in IF_ELSE_LIST:
					return &"'else' without matching 'if' statement"
				var previous_sibling_result:IF_ELSE_STATE = context.node_data[prev_sibling].result
				if previous_sibling_result == IF_ELSE_STATE.FAILED:
					next(ast_node.get_next_node())
					return &""
				
				# if reached this point, it means we've attempted to evaluate or previous is a pass
				var next_sibling := ast_node.get_next_sibling()
				if next_sibling:
					next(next_sibling)
				else:
					next(ast_node.get_next_node(false))
		AST_TYPE_NOOP:
			# just move on to the next node, these children
			# will have been handled at the parent level
			next(ast_node.get_next_node(false)) 
		AST_TYPE_UNSPECIFIED when (ast_node.parent.type in [AST_TYPE_SPEAK, AST_TYPE_COMMAND]):
			# just move on to the next node, these children
			# will have been handled at the parent level
			next(ast_node.get_next_node(false)) 
			
		_:
			BdbError.not_implemented("Unsupported ast-node type %s " % ast_node.type)
